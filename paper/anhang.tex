\appendix

\chapter{Grundlagen der Computertomographie}

\section{Herleitung des Fourier-Scheiben-Theorems}\label{app:fourier_scheibe}

Seien $F(u, v)$ die zweidimensionale
Fouriertransformation des Objekts $f(x, y)$:

\begin{equation}\label{eq:obj_2d_full}
    F(u, v) = \int\limits_{-\infty}^{\infty} \int\limits_{-\infty}^{\infty} f(x, y) \cdot e^{-2 \pi i \cdot(u x + v y)}\
        \mathrm{d} x\ \mathrm{d} y
\end{equation}

und $S_{\theta}(w)$ die eindimensionale Fouriertransformation der Projektion unter dem Winkel $\theta$ $P_{\theta}(t)$:

\begin{equation}\label{eq:proj_fft}
    S_{\theta}(w) = \int\limits_{-\infty}^{\infty} P_{\theta}(t) \cdot e^{-2 \pi i \cdot w t}\ \mathrm{d} t
\end{equation}

Im Folgenden sei der Fall $\theta = 0$ betrachtet. Setzt man $v = 0$, so wird die Formel~\ref{eq:obj_2d_full}
vereinfacht:

\begin{equation}
    F(u, 0) = \int\limits_{-\infty}^{\infty} \int\limits_{-\infty}^{\infty} f(x, y) \cdot e^{-2 \pi i \cdot u x}\
    \mathrm{d} x\ \mathrm{d} y
\end{equation}

Der Phasenfaktor ist nun nicht mehr von $y$ abhängig, wodurch das Integral in zwei Hälften geteilt werden kann:

\begin{equation}\label{eq:obj_2d_split}
    F(u, 0) = \int\limits_{-\infty}^{\infty} \left[\int\limits_{-\infty}^{\infty} f(x, y)\ \mathrm{d} y \right] \cdot
    e^{-2 \pi i \cdot u x}\ \mathrm{d} x
\end{equation}

Der Term in eckigen Klammern entspricht der Gleichung für eine Projektion entlang konstanter $x$-Linien:

\begin{equation}
    P_{\theta = 0}(x) = \int\limits_{-\infty}^{\infty} f(x, y)\ \mathrm{d} y
\end{equation}

Durch Einsetzen in Gleichung~\ref{eq:obj_2d_split} erhält man:

\begin{equation}
    F(u, 0) = \int\limits_{-\infty}^{\infty} P_{\theta = 0}(x) \cdot e^{-2 \pi i \cdot u x}\ \mathrm{d} x
\end{equation}

Die rechte Seite dieser Gleichung stellt die eindimensional fouriertransformierte Projektion $P_{\theta = 0}$ dar (vgl.
Gleichung~\ref{eq:proj_fft}), es ergibt sich also der folgende Zusammenhang zwischen der transformierten Projektion und
dem zweidimensional fouriertransformierten Objekt:

\begin{equation}
    F(u, 0) = S_{\theta = 0}(u)
\end{equation}

Dieses Ergebnis ist die einfachste Form des Fourier-Scheiben-Theorems. Darüber hinaus ist es unabhängig von der
konkreten Konstellation zwischen dem Objekt und dem Koordinatensystem der Projektion. Wird das
$(t, s)$-Koordinatensystem um den Winkel $\theta$ rotiert, so ist die Fouriertransformation der Projektion gleich der
zweidimensionalen Fouriertransformation des Objekts entlang einer radialen Linie, die um den Winkel $\theta$ rotiert
wird (siehe Abbildung~\ref{fig:fourier_scheibe}, vgl.~\cite{rosenkak}, S. 366).

\chapter{Implementierung}

\section{Implementierung der Filterung}

\begin{code}
\begin{minted}[breaklines,breakafter=\,,fontsize=\small]{cuda}
__global__ void filter_creation_kernel(float* __restrict__ r,
    const std::int32_t* __restrict__ j, std::uint32_t size, float tau)
{
    auto x = blockIdx.x * blockDim.x + threadIdx.x;

    if(x < size)
    {
        if(j[x] == 0)
            r[x] = (1.f / 8.f) * (1.f / powf(tau, 2.f));
        else
        {
            if(j[x] % 2 == 0)
                r[x] = 0.f;
            else
                r[x] = -(1.f / (2.f * powf(j[x], 2.f)
                        * powf(M_PI, 2.f)
                        * powf(tau, 2.f)));
        }
    }
}
\end{minted}
\captionof{listing}{Filtergenerierung}
\label{app:filter_gen}
\end{code}

\begin{code}
\begin{minted}[escapeinside=||,fontsize=\small]{cuda}
__global__ void normalization_kernel(cufftReal* dst,
    std::size_t pitch, std::uint32_t N_h,
    std::uint32_t N_v, std::uint32_t N_hFFT)
{
    auto s = blockIdx.x * blockDim.x + threadIdx.x;
    auto t = blockIdx.y * blockDim.y + threadIdx.y;

    if((s < N_h) && (t < N_v))
    {
        auto row = |\textbf{\textcolor{keyword-green}{reinterpret\_cast}}|<cufftReal*>(
            |\textbf{\textcolor{keyword-green}{reinterpret\_cast}}|<char*>(dst) + t * pitch);

        row[s] /= N_hFFT;
    }
}
\end{minted}
\captionof{listing}{Projektionsnormalisierung}
\label{app:filter_norm}
\end{code}

\section{Implementierung der gefilterten Rückprojektion}

\begin{code}
\begin{minted}[breaklines,breakafter=\,,fontsize=\small]{cuda}
__device__ auto vol_centered_coordinate(unsigned int coord,
                                        std::uint32_t dim,
                                        float size)
    -> float
{
    auto size2 = size / 2.f;
    return -(dim * size2) + size2 + coord * size;
}
\end{minted}
\captionof{listing}{Koordinatensystemtransformation im Volumen}
\label{app:coord_vol}
\end{code}

\begin{code}
\begin{minted}[breaklines,breakafter=\,,fontsize=\small]{cuda}
__device__ auto proj_pixel_coordinate(float coord, std::uint32_t dim,
                                      float size, float offset)
    -> float
{
    auto size2 = size / 2.f;
    auto min = -(dim * size2) - offset;
    return (coord - min) / size - (1.f / 2.f);
}
\end{minted}
\captionof{listing}{Koordinatensystemtransformation auf dem Detektor}
\label{app:coord_det}
\end{code}

\begin{code}
\begin{minted}[breaklines,breakafter=\,,fontsize=\small]{c++}
struct backprojection_constants
{
    /* Volumenkonstanten */
    std::uint32_t N_x;          // Voxelzahl in x-Richtung
    std::uint32_t N_y;          // Voxelzahl in y-Richtung
    std::uint32_t N_z;          // Voxelzahl in z-Richtung

    float d_x;                  // Voxelgröße in x-Richtung
    float d_y;                  // Voxelgröße in y-Richtung
    float d_z;                  // Voxelgröße in z-Richtung

    /* Projektionskonstanten */
    std::uint32_t N_h;          // Pixelzahl in x-Richtung
    std::uint32_t N_v;          // Pixelzahl in y-Richtung

    float d_h;                  // Pixelgröße in x-Richtung
    float d_v;                  // Pixelgröße in y-Richtung

    /* Detektorkonstanten */
    float delta_h;              // horizontaler Offset
    float delta_v;              // vertikaler Offset

    float d_so;                 // Abstand Quelle - Objekt
    float d_sd;                 // Abstand Quelle - Detektor
};
\end{minted}
\caption{Struktur der Rückprojektions-Konstanten}
\label{app:fdk_consts}
\end{code}

\begin{code}
\begin{minted}[breaklines,breakafter=\,,escapeinside=||,fontsize=\small]{cuda}
__global__ void backprojection_kernel(
    float* vol, std::size_t vol_pitch, cudaTextureObject_t proj,
    float theta_sin, float theta_cos)
{
    auto k = blockIdx.x * blockDim.x + threadIdx.x;
    auto l = blockIdx.y * blockDim.y + threadIdx.y;
    auto m = blockIdx.z * blockDim.z + threadIdx.z;

    if((k < consts.N_x) && (l < consts.N_y) &&
       (m < consts.vol_dim_z)) {
        // berechne gegenwärtige Schicht und Zeile
        auto slice_pitch = vol_pitch * consts.N_y;
        auto slice = |\textbf{\textcolor{keyword-green}{reinterpret\_cast}}|<char*>(vol) + m * slice_pitch;
        auto row = |\textbf{\textcolor{keyword-green}{reinterpret\_cast}}|<float*>(slice + l * vol_pitch);

        // lade alten Wert aus dem globalen Speicher
        auto old_val = row[k];

        // Koordinatensystemursprung in Volumenmittelpunkt verschieben
        auto x = vol_centered_coordinate(k, consts.N_x, consts.d_x);
        auto y = vol_centered_coordinate(l, consts.N_y, consts.d_y);
        auto z = vol_centered_coordinate(m, consts.N_z, consts.d_z);

        // Koordinaten rotieren
        auto s = x * theta_cos + y * theta_sin;
        auto t = -x * theta_sin + y * theta_cos;

        // projiziere rotierte Koordinaten auf Detektor
        auto factor = consts.d_sd / (s + consts.d_so);
        auto h = proj_pixel_coordinate(t * factor, consts.N_h,
             consts.d_h, consts.delta_h) + 0.5f;
        auto v = proj_pixel_coordinate(z * factor, consts.N_v,
             consts.d_v, consts.delta_v) + 0.5f;

        // lies Projektionswert an dieser Stelle
        auto det = tex2D<float>(proj, h, v);

        // Rückprojektion
        auto u = -(consts.d_so / (s + consts.d_so));
        row[k] = old_val + 0.5f * det * u * u;
    }
}
\end{minted}
\captionof{listing}{Vollständiger Rückprojektions-\gls{kernel}}
\label{app:impl_bp}
\end{code}

